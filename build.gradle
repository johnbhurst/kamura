// Copyright 2015 John Hurst
// John Hurst (john.hurst@gmail.com)
// 2015-05-14

// TODO: ASF notice

description = "Groovy builders for some common object types"

wrapper {
  gradleVersion = "2.12"
}

subprojects {
  apply plugin: "groovy"
}

def versionProps = new Properties()
new File(rootDir, "version.properties").withReader {versionProps.load(it)}
allprojects {
  versionProps.each {k, v ->
    project.ext[k + "Version"] = v
  }
  group = "org.kamura"
  version = "0.0.1"
  status = "SNAPSHOT"
  repositories {
    jcenter()
    mavenCentral()
    mavenCentral(url: "http://jpdfunit.sourceforge.net/repo")
  }
}

subprojects {
  dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"

    testCompile("org.spockframework:spock-core:$spockVersion") {
      exclude group: "org.codehaus.groovy"
    }
  }

  jar {
    def buildString = version + " built on " +
      new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("UTC"))
    manifest {
      attributes(
        "Built-By": System.properties["user.name"],
        "Implementation-Title": project.name,
        "Implementation-Version": buildString
      )
    }
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }
}
