// Copyright 2016 John Hurst
// John Hurst (john.hurst@gmail.com)
// 2015-05-14

// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import java.text.SimpleDateFormat

description = "Groovy builders for some common object types"

def javaVersion = project.hasProperty("javaVersion") ? project.javaVersion : "17"

allprojects {
  project.rootProject.properties.each {property, value ->
    if (property.endsWith("Version")) {
      project.ext[property] = value
    }
  }
  group = "nz.kamura"
  version = "0.0.11"
  repositories {
    mavenCentral()
    mavenCentral(url: "https://jpdfunit.sourceforge.net/repo")
  }
}

subprojects {
  apply plugin: "java-library"
  apply plugin: "maven-publish"

  jar {
    manifest {
      attributes(
        "Build-Jdk": javaVersion,
        "Build-OS": "${System.getProperty("os.name")} ${System.properties["os.arch"]} (${System.getProperty("os.version")})",
        "Build-Timestamp": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss'Z'").format(new Date()),
        "Built-By": System.getProperty("user.name"),
        "Built-With-Gradle": "${gradle.gradleVersion}",
        "Created-By": "$javaVersion (${System.getProperty("java.vendor")})",
        "Implementation-Title": project.name,
        "Implementation-Version": archiveVersion,
      )
    }
  }

  java {
    withSourcesJar()
    toolchain {
      languageVersion = JavaLanguageVersion.of(javaVersion)
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        pom {
          name = project.name
          description = project.description
          url = "https://github.com/johnbhurst/kamura"
          licenses {
            license {
              name = "Apache License, Version 2.0"
              url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
          developers {
            developer {
              id = "johnhurst"
              name = "John Hurst"
              email = "john.b.hurst@gmail.com"
            }
          }
        }
      }
    }
    repositories {
      maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY")
        credentials {
          username = System.getenv("GITHUB_ACTOR")
          password = System.getenv("GITHUB_TOKEN")
        }
      }
    }
  }
}
