// Copyright 2016 John Hurst
// John Hurst (john.hurst@gmail.com)
// 2015-05-14

// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

description = "Groovy builders for some common object types"

wrapper {
  gradleVersion = "2.12"
}

subprojects {
  apply plugin: "groovy"
}

def versionProps = new Properties()
new File(rootDir, "version.properties").withReader {versionProps.load(it)}
allprojects {
  versionProps.each {module, version ->
    project.ext[module] = version
  }
  group = "org.kamura"
  version = "0.0.1"
  status = "SNAPSHOT"
  repositories {
    jcenter()
    mavenCentral()
    mavenCentral(url: "http://jpdfunit.sourceforge.net/repo")
  }
}

subprojects {
  dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"

    testCompile("org.spockframework:spock-core:$spockVersion") {
      exclude group: "org.codehaus.groovy"
    }
  }

  jar {
    def buildString = version + " built on " +
      new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("UTC"))
    manifest {
      attributes(
        "Built-By": System.properties["user.name"],
        "Implementation-Title": project.name,
        "Implementation-Version": buildString
      )
    }
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }
}
