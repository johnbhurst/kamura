
apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "idea"

group = "nz.co.skepticalhumorist"
version = "00.00.07"
status = 'release'

repositories {
  mavenCentral name: "jpdfunit-repo", urls: "http://jpdfunit.sourceforge.net/repo"
  mavenCentral name: "jboss", urls: "http://repository.jboss.com/maven2/"
}

dependencies {
  groovy group: "org.codehaus.groovy", name: "groovy", version: "1.7.5"
  compile group: "com.lowagie", name: "itext", version: "2.1.7"
  testCompile group: "junit", name: "junit", version: "4.8.1"
  testCompile group: "net.sf.jpdfunit", name: "jpdfunit", version: "1.1"
  testRuntime group: "pdfbox", name: "pdfbox", version: "0.7.3"  // why is this not included by jpdfunit???
  testRuntime group: "jfree", name: "jfreechart", version: "1.0.12"
  testRuntime files("lib/itext-hyph-xml.jar")
  testRuntime files("lib/iTextAsian.jar")
}

uploadDefault {
  uploadDescriptor = true
  repositories {
    add(Class.forName("org.apache.ivy.plugins.resolver.FileSystemResolver").newInstance()) {
      name = 'repo'
      addIvyPattern "$projectDir/repo/[organisation]/[module]/[revision]/ivy.xml"
      addArtifactPattern "$projectDir/repo/[organisation]/[module]/[revision]/[type]s/[artifact].[ext]"
      checkmodified = true
    }
  }
}

void runExamples(String path) {
  new File("build/$path").mkdirs()
  def gse = new GroovyScriptEngine([path] as String[])
  new File(path).eachFileMatch(~/^.*\.groovy$/) {File file ->
    gse.run(file.name, new Binding())
  }
}

// This task runs the examples as-is, fetching dependencies from a repo via Grape.
task runExamples << {
  runExamples("examples/classroom/intro")
  runExamples("examples/questions/graphics2D")
}

// This task creates copies of the example scripts with @Grab annotations removed.
// These can be run by tests, fetching classes from the test classpath.
task filterExamples(type: Copy) {
  from "examples"
  into "build/examples"
  filter {String line ->
    line.startsWith("@Grab") ? "" : line
  }
}

test.dependsOn filterExamples

test {
  systemProperties 'itext.examples.home':   pdfBuilderITextExamplesHome
  systemProperties 'windows.font.location': pdfBuilderWindowsFontLocation
}

